on run
	set folderPath to choose folder with prompt "Select a folder containing an API request to broadcast" default location (path to me)
	processFolder(folderPath)
end run

on open theFiles
	if (count theFiles) is not equal to 1 then
		display dialog "Please drop only one API request folder at a time." buttons {"OK"} default button "OK" with title "API Request Broadcaster" with icon stop
		return
	end if

	set folderPath to item 1 of theFiles
	processFolder(folderPath)
end open

on processFolder(folderPath)
	-- Set variables
	set datasitesPath to quoted form of "{{DATASITES_PATH}}"

	-- Convert the folder path to POSIX format for shell compatibility
	set folderPOSIXPath to POSIX path of folderPath

	-- Check if the item is a folder and contains the 'run.sh' file using shell commands
	set folderCheck to do shell script "if [ -d " & quoted form of folderPOSIXPath & " ] && [ -f " & quoted form of folderPOSIXPath & "/run.sh ]; then echo 'valid'; else echo 'invalid'; fi"

	if folderCheck is not "valid" then
		display dialog "Invalid API request. Please ensure the selected item is a folder containing a 'run.sh' file." buttons {"OK"} default button "OK" with title "API Request Broadcaster" with icon stop
		return
	end if

	-- Get number of datasites with inbox installed
	set datasitesCount to do shell script "count=0; for d in " & datasitesPath & "/*/; do [ -d \"$d/inbox\" ] && ((count++)); done; echo $count"
	set datasiteLabel to "datasite"
	if datasitesCount is greater than 1 then set datasiteLabel to "datasites"

	-- Show confirmation dialog
	set userResponse to display dialog "You are about to broadcast the API request '" & (do shell script "basename " & quoted form of folderPOSIXPath) & "' to " & datasitesCount & " " & datasiteLabel & ". This action will notify multiple users across various locations. Are you sure you want to proceed?" buttons {"Cancel", "Schedule Broadcast"} default button "Schedule Broadcast" with title "Confirmation Needed" with icon caution

	if button returned of userResponse is "Schedule Broadcast" then
		set folderPOSIXPathWithoutTrailingSlash to do shell script "echo " & quoted form of folderPOSIXPath & " | sed 's|/$||'"

		-- Copy the folder to each inbox sub-directory within datasitesPath (only if inbox directory exists)
		do shell script "for d in " & datasitesPath & "/*/inbox; do [ -d \"$d\" ] && cp -R " & quoted form of folderPOSIXPathWithoutTrailingSlash & " \"$d\"; done"

		display dialog "The API request has been successfully scheduled to broadcast to " & datasitesCount & " " & datasiteLabel &". You can monitor the progress in the SyftBox logs." buttons {"OK"} default button "OK" with title "API Request Broadcasted" with icon note
	end if
end processFolder
